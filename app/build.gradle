apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-kapt"
apply plugin: 'com.google.firebase.crashlytics'

repositories {
    mavenCentral()
}


android {
    def googleMapsApiKeyDevelop = ""
    def googleMapsApiKeyStore = ""

    Properties props = new Properties()
    def propFile = file('../signing.properties')
    if (propFile.canRead() && props != null) {
        props.load(new FileInputStream(propFile))
        if (props.containsKey('GOOGLE_MAPS_API_KEY_DEVELOP')) {
            googleMapsApiKeyDevelop = file(props['GOOGLE_MAPS_API_KEY_DEVELOP'])
        }
        if (props.containsKey('GOOGLE_MAPS_API_KEY_STORE')) {
            googleMapsApiKeyStore = file(props['GOOGLE_MAPS_API_KEY_STORE'])
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    compileSdkVersion appCompileSdk
    buildToolsVersion '30.0.2'
    defaultConfig {
        multiDexEnabled true
        applicationId "de.p72b.mocklation"
        minSdkVersion appMinSdk
        targetSdkVersion 30
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "ROOM_VERSION", '"' + rootProject.roomVersion + '"'
        buildConfigField "String", "PLAY_SERVICES_VERSION", '"' + playServicesVersion + '"'
        buildConfigField "String", "PLAY_SERVICES_LOCATION_VERSION", '"' + playServicesLocationVersion + '"'
        buildConfigField "String", "MAP_UTILITIES_VERSION", '"' + mapsUtilsVersion + '"'
        buildConfigField "String", "GSON_VERSION", '"' + gsonVersion + '"'
        buildConfigField "String", "FIREBASE_VERSION", '"' + firebaseVersion + '"'
        buildConfigField "String", "RX_JAVA_VERSION", '"' + rxjavaVersion + '"'
        buildConfigField "String", "RX_ANDROID_VERSION", '"' + rxandroidVersion + '"'
        buildConfigField "String", "BROWSER_VERSION", '"' + browserVersion + '"'
        buildConfigField "String", "MATERIAL_VERSION", '"' + materialVersion + '"'
        buildConfigField "String", "DYNAMIC_ANIMATION_VERSION", '"' + dynamicAnimationVersion + '"'
        buildConfigField "String", "CARD_VIEW_VERSION", '"' + cardViewVersion + '"'
        buildConfigField "String", "APP_COMPAT_VERSION", '"' + appCompatVersion + '"'
    }

    signingConfigs {
        release {
            storeFile file('../mocklation-release-key.keystore')
            storePassword ' '
            keyAlias "mocklation"
            keyPassword ' '
        }
        debug {
            storeFile file('../mocklation-debug-key.keystore')
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    flavorDimensions "default"

    productFlavors {
        develop {
            dimension "default"
            applicationId "de.p72b.mocklation.develop"
            versionCode appVersionCode
            versionName "${appVersionName} develop"

            resValue "string", "google_maps_api_key", googleMapsApiKeyDevelop
        }
        store {
            dimension "default"
            applicationId "de.p72b.mocklation"
            versionCode appVersionCode
            versionName appVersionName

            resValue "string", "google_maps_api_key", googleMapsApiKeyStore
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.cardview:cardview:$cardViewVersion"
    implementation "androidx.dynamicanimation:dynamicanimation:$dynamicAnimationVersion"
    implementation "androidx.browser:browser:$browserVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.preference:preference-ktx:$preferenceVersion"

    implementation "com.google.android.material:material:$materialVersion"
    implementation "com.google.android.gms:play-services-maps:$playServicesVersion"
    implementation "com.google.android.gms:play-services-places:$playServicesVersion"
    implementation "com.google.android.gms:play-services-location:$playServicesLocationVersion"
    implementation "com.google.maps.android:android-maps-utils:$mapsUtilsVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"

    implementation platform("com.google.firebase:firebase-bom:$firebaseVersion")
    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'

    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroidVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.github.p72b:locator-library:1.6.0'
}
apply plugin: 'com.google.gms.google-services'
